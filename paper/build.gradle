plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

dependencies {
    compileOnly "com.destroystokyo.paper:paper-api:1.16.5-R0.1-SNAPSHOT"
    compileOnly "me.clip:placeholderapi:2.11.6"
    compileOnly "dev.jorel:commandapi-bukkit-core:9.4.1"

    implementation project(":api")

    implementation "org.javatuples:javatuples:1.2"
    implementation "me.lucko:helper:5.6.14"
    implementation "dev.triumphteam:triumph-gui:3.1.10"
    implementation "com.google.inject:guice:7.0.0"
    implementation "com.google.inject.extensions:guice-assistedinject:7.0.0"
    implementation "net.kyori:adventure-api:4.17.0"
    implementation "net.kyori:adventure-text-minimessage:4.17.0"
    implementation "net.kyori:adventure-text-serializer-legacy:4.17.0"
    implementation "net.kyori:adventure-platform-bukkit:4.3.3"

    implementation "me.wyne:WUtils-config:2.5.2"
    implementation "me.wyne:WUtils-log:3.0.3"
    implementation "me.wyne:WUtils-i18n:2.1.0"

    testImplementation(platform("org.junit:junit-bom:5.10.2"))
    testImplementation "org.junit.jupiter:junit-jupiter:5.10.2"
    testImplementation "com.github.MockBukkit:MockBukkit:v1.16-SNAPSHOT"
}

shadowJar {
    archiveBaseName.set("WorldEvents")
    archiveClassifier.set("")
    minimize()
    relocate "org.javatuples", "org.nevermined.worldevents.shadow.javatuples"
    relocate "me.lucko.helper", "org.nevermined.worldevents.shadow.lucko.helper"
    relocate "dev.triumphteam.gui", "org.nevermined.worldevents.shadow.triumphteam.gui"
    relocate "com.google.inject", "org.nevermined.worldevents.shadow.google.guice"
    relocate "com.google.common", "org.nevermined.worldevents.shadow.google.common"
    relocate "net.kyori", "org.nevermined.worldevents.shadow.kyori"
    relocate "me.wyne.wutils", "org.nevermined.worldevents.shadow.wutils"
}

def targetJavaVersion = 16
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

test {
    useJUnitPlatform()
}