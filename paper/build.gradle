plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'xyz.jpenilla.run-paper' version '2.3.1'
}

dependencies {
    compileOnly "com.destroystokyo.paper:paper-api:1.16.5-R0.1-SNAPSHOT"
    compileOnly "me.clip:placeholderapi:2.11.6"
    implementation "dev.jorel:commandapi-bukkit-shade:9.5.3"

    implementation project(":api")

    implementation "org.javatuples:javatuples:1.2"
    implementation "commons-io:commons-io:2.18.0"
    implementation "me.lucko:helper:5.6.14"
    implementation("dev.triumphteam:triumph-gui:3.1.10") {
        exclude group: "net.kyori"
    }
    implementation "com.google.inject:guice:7.0.0"
    implementation "com.google.inject.extensions:guice-assistedinject:7.0.0"
    implementation "net.kyori:adventure-text-serializer-legacy:4.7.0"
    implementation "net.kyori:adventure-text-serializer-plain:4.7.0"

    implementation "me.wyne:WUtils-config:2.9.6"
    implementation "me.wyne:WUtils-configurables:1.15.6"
    implementation "me.wyne:WUtils-log:3.4.8"
    implementation("me.wyne:WUtils-i18n:5.1.17") {
        exclude group: "net.kyori"
    }
}

shadowJar {
    archiveBaseName.set("WorldEvents")
    archiveClassifier.set("")
    def outputDir = project.hasProperty('shadowJarOutputDir') ? project.shadowJarOutputDir : "${buildDir}/libs"
    destinationDirectory = file(outputDir)
    minimize()
    relocate "dev.jorel.commandapi", "org.nevermined.worldevents.shadow.commandapi"
    relocate "org.javatuples", "org.nevermined.worldevents.shadow.javatuples"
    relocate "me.lucko.helper", "org.nevermined.worldevents.shadow.lucko.helper"
    relocate "dev.triumphteam.gui", "org.nevermined.worldevents.shadow.triumphteam.gui"
    relocate "com.google.inject", "org.nevermined.worldevents.shadow.google.guice"
    relocate "com.google.common", "org.nevermined.worldevents.shadow.google.common"
    relocate "me.wyne.wutils", "org.nevermined.worldevents.shadow.wutils"
}

runServer {
    downloadPlugins {
        url("https://ci.extendedclip.com/view/Plugins/job/PlaceholderAPI/197/artifact/build/libs/PlaceholderAPI-2.11.6.jar")
        url("https://download.luckperms.net/1604/bukkit/loader/LuckPerms-Bukkit-5.5.15.jar")
        github("dmulloy2", "ProtocolLib", "5.4.0", "ProtocolLib.jar")
        github("ViaVersion", "ViaVersion", "5.2.1", "ViaVersion-5.2.1.jar")
        github("ViaVersion", "ViaBackwards", "5.2.1", "ViaBackwards-5.2.1.jar")
        github("CommandAPI", "CommandAPI", "9.4.2", "CommandAPI-9.4.2.jar")
    }
    minecraftVersion("1.16.5")
}

tasks.withType(xyz.jpenilla.runtask.task.AbstractRun).configureEach {
    javaLauncher = javaToolchains.launcherFor {
        vendor = JvmVendorSpec.JETBRAINS
        languageVersion = JavaLanguageVersion.of(21)
    }
    jvmArgs '-XX:+AllowEnhancedClassRedefinition', "-DPaper.IgnoreJavaVersion=true"
}

def targetJavaVersion = 16
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}